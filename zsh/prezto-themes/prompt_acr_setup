#
# A theme based on steeef theme
#  * RVM/Rbenv info shown on the right
#  * Git branch info on the left
#  * Single line prompt
#
# Authors:
#   Steve Losh <steve@stevelosh.com>
#   Bart Trojanowski <bart@jukie.net>
#   Brian Carper <brian@carper.ca>
#   steeef <steeef@gmail.com>
#   Sorin Ionescu <sorin.ionescu@gmail.com>
#   Yan Pritzker <yan@pritzker.ws>

function prompt_skwp_precmd {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS

  # Get Git repository information.
  if (( $+functions[git-info] )); then
    git-info on
    git-info
  fi

  # Get ruby information
  if (( $+functions[ruby-info] )); then
    ruby-info
  fi
}

function prompt_skwp_setup {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS
  prompt_opts=(cr percent subst)

  autoload -Uz add-zsh-hook

  add-zsh-hook precmd prompt_skwp_precmd

  # Use extended color pallete if available.
  if [[ $TERM = *256color* || $TERM = *rxvt* ]]; then
    __PROMPT_SKWP_COLORS=(
      "%F{81}"  # turquoise
      "%F{166}" # orange
      "%F{135}" # purple
      "%F{161}" # hotpink
      "%F{118}" # limegreen
    )
  else
    __PROMPT_SKWP_COLORS=(
      "%F{cyan}"
      "%F{black}"
      "%K{blue}%F{black}"
      "%F{red}"
      "%F{yellow}"
      "%F{green}"
      "%K{boldblack}%F{blue}"
      "%K{blue}%B%F{black}"
      "%K{black}%B%F{blue}"
    )
  fi

  ADD_COL="%F{green}"
  DEL_COL="%F{black}"
  MOD_COL="%F{red}"
  UNT_COL="%F{yellow}"
  NAME_COL="%K{black}%B%F{blue}"
  NAME_DIR_SEP="%K{blue}%F{black}"
  DIR_COL="%K{blue}%F{black}"
  DIR_END_COL="%K{black}%F{blue}"

  SEGMENT_SEPARATOR='⮀'

  # git
  zstyle ':prezto:module:git:info:branch' format "${__PROMPT_SKWP_COLORS[3]}%b%f"
  zstyle ':prezto:module:git:info:added' format "$ADD_COL●%f"
  zstyle ':prezto:module:git:info:deleted' format "$DEL_COL●%f"
  zstyle ':prezto:module:git:info:modified' format "$MOD_COL●%f"
  zstyle ':prezto:module:git:info:untracked' format "$UNT_COL●%f"
  zstyle ':prezto:module:git:info:keys' format 'prompt' "${__PROMPT_SKWP_COLORS[8]}$SEGMENT_SEPARATOR %b%d%a%m%u ${__PROMPT_SKWP_COLORS[7]}$SEGMENT_SEPARATOR "

  # ruby info (rvm, rbenv)
  zstyle ':prezto:module:ruby:info:version' format '[%v]'

  PROMPT="$NAME_COL%n@%m $NAME_DIR_SEP$SEGMENT_SEPARATOR %~ $DIR_END_COL$SEGMENT_SEPARATOR "'$git_info[prompt]'"$f"
  RPROMPT='%F{blue}${ruby_info[version]}'
}

prompt_skwp_setup "$@"
